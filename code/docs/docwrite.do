* some webdoc options to deal with formatting
webdoc set stlog
webdoc set _stlog

* grab variable info
local label: variable label `1'
local labelname: value label `1'
tempfile thelevels
if "`labelname'" ~= "" valuelabel2md using `thelevels', label(`labelname')
* variable availability time periods
preserve
import delimited using ${codedocs}variables_groups.csv, clear varnames(1)
keep if varname == "`1'"
assert _N == 1
foreach dataset in basic march may org {
	local `dataset'years: di `dataset'
}
restore

* prepare variable and figure code for formatting (4 spaces)
tempfile varcode
copy ${codevars}generate_`1'.do `varcode'
!sed -i -e 's/^/    /' `varcode'
* prepare figure code for formatting (4 spaces)
capture confirm file ${variableimages}`1'_titleimage.svg
if _rc == 0 {
	tempfile figcode
	copy ${codedocs}analysis/`1'_analysis.do `figcode'
	!sed -i -e 's/^/    /' `figcode'
}


* preamble for page:
webdoc put ---
webdoc put notes: This page is generated by createdocs.do. Do not manually edit this page.
webdoc put ---
webdoc put 

* Title section
webdoc put # `1': `label'

* Title image
capture confirm file ${variableimages}`1'_titleimage.svg
if _rc == 0 {
	webdoc put ![`1' title image](images/`1'_titleimage.svg)
	webdoc put
}

* Description
webdoc put ## Description
webdoc append ${variableshortdesc}`1'_shortdesc.md
webdoc put

* Availability
webdoc put ## Availability
webdoc put Sample | Years
webdoc put -------|------
if "`basicyears'" ~= "" webdoc put Basic | `basicyears'
if "`mayyears'" ~= "" webdoc put May | `mayyears'
if "`orgyears'" ~= "" webdoc put ORG | `orgyears'
if "`marchyear'" ~= "" webdoc put March | `marchyears'
webdoc put
capture confirm file ${variableavailability}`1'_availability.md
if _rc == 0 webdoc append ${variableavailability}`1'_availability.md
webdoc put

* Value labels
if "`labelname'" != "" {
	webdoc put ## Values
	webdoc append `thelevels'
	webdoc put 
}

* Detailed comments
capture confirm file ${variablelongdesc}`1'_longdesc.md
if _rc == 0 {
	webdoc put ## Detailed comments
	webdoc append ${variablelongdesc}`1'_longdesc.md
	webdoc put
}

* Code for variable generation
webdoc put ## Code
webdoc put ??? note "Variable creation"
webdoc put     ```stata
webdoc append `varcode'
webdoc put     ```
webdoc put 

* Code for detailed analysis
capture confirm file ${variableimages}`1'_titleimage.svg
if _rc == 0 {
	webdoc put ??? note "Figure creation"
	webdoc put     ```stata
	webdoc append `figcode'
	webdoc put     ```
	webdoc put 
}
