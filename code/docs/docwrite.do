* some webdoc options to deal with formatting
webdoc set stlog
webdoc set _stlog

* grab variable info
local label: variable label `1'
local labelname: value label `1'
if "`labelname'" ~= "" valuelabel2csv using ${variablelevels}`1'.csv, label(`labelname') replace
* variable availability time periods
preserve
import delimited using ${codedocs}variables_groups.csv, clear varnames(1)
keep if varname == "`1'"
assert _N == 1
foreach dataset in basic march may org {
	local `dataset'years: di `dataset'
}
restore


* preamble for page:
/***
.. This page is generated by createdocs.do. Do not manually edit this page.

***/

* Title section

webdoc put ================================================================================
webdoc put `1': `label'
webdoc put ================================================================================
webdoc put

* Title image
capture confirm file ${variableimages}`1'_titleimage.svg
if _rc == 0 {
	webdoc put .. image:: images/`1'_titleimage.svg
	webdoc put
}

* Description
webdoc put Description
webdoc put ================================================================================
webdoc append ${variableshortdesc}`1'_shortdesc.rst
webdoc put

* Availability
webdoc put Availability
webdoc put ================================================================================
webdoc put .. csv-table::
webdoc put   :header: "Sample", "Years"
webdoc put   :widths: 20, 40
webdoc put
if "`basicyears'" ~= "" webdoc put   "Basic","`basicyears'"
if "`mayyears'" ~= "" webdoc put   "May","`mayyears'"
if "`orgyears'" ~= "" webdoc put   "ORG","`orgyears'"
webdoc put
capture confirm file ${variableavailability}`1'_availability.rst
if _rc == 0 webdoc append ${variableavailability}`1'_availability.rst
webdoc put

* Value labels
if "`labelname'" != "" {
	webdoc put Values
	webdoc put ================================================================================
	webdoc put .. csv-table::
	webdoc put    :file: levels/`1'.csv
	webdoc put
}

* Detailed comments
capture confirm file ${variablelongdesc}`1'_longdesc.rst
if _rc == 0 {
	webdoc put Detailed comments
	webdoc put ================================================================================
	webdoc append ${variablelongdesc}`1'_longdesc.rst
	webdoc put
}

* Code for variable generation
webdoc put Code
webdoc put ================================================================================
webdoc put .. toggle-header::
webdoc put    :header: Variable creation (**show/hide code**)
webdoc put
webdoc put    .. literalinclude:: code/generate_`1'.do
webdoc put

* Code for detailed analysis
capture confirm file ${variableimages}`1'_titleimage.svg
if _rc == 0 {
	webdoc put .. toggle-header::
	webdoc put    :header: Figure above (**show/hide code**)
	webdoc put
	webdoc put    .. literalinclude:: code/`1'_analysis.do
	webdoc put
}

* Variable notes
webdoc put Variable notes
webdoc put ================================================================================
webdoc stlog, cmdstrip raw
notes `1'
webdoc stlog close
